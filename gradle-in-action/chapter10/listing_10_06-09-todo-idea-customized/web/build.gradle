plugins {
    id "org.akhikhl.gretty" version "2.0.0"
}
apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'
apply plugin: 'groovy'

repositories {
    //mavenCentral()
    maven { url 'https://maven.aliyun.com/repository/central' }
}

configurations {
    functTestCompile.extendsFrom testCompile
    functTestRuntime.extendsFrom testRuntime
}

ext.seleniumGroup = 'org.seleniumhq.selenium'
ext.seleniumVersion = '3.141.59'

dependencies {
    compile project(':repository')
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
    runtime 'javax.servlet:jstl:1.2'
    runtime 'taglibs:standard:1.1.2'
    testCompile 'org.codehaus.groovy:groovy:2.4.21'
    testCompile 'junit:junit:4.11'
    functTestCompile 'org.codehaus.geb:geb-junit4:0.7.2'
    functTestCompile "$seleniumGroup:selenium-api:$seleniumVersion"
    functTestRuntime "$seleniumGroup:selenium-firefox-driver:$seleniumVersion"
}

sourceSets {
    functionalTest {
        groovy.srcDir file('src/functTest/groovy')
        resources.srcDir file('src/functTest/resources')
        compileClasspath = sourceSets.main.output + configurations.functTestCompile
        runtimeClasspath = output + compileClasspath + configurations.functTestRuntime
    }
}

task functionalTest(type: Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
    reports.html.destination = file("$reports.html.destination/functional")
    reports.junitXml.destination = file("$reports.junitXml.destination/functional")
    systemProperty 'geb.env', 'firefox'
    systemProperty 'geb.build.reportsDir', reporting.file("$name/geb")
}

check.dependsOn functionalTest

idea {
    module {
        sourceSets.functionalTest.allSource.srcDirs.each {
            testSourceDirs += it
        }

//    scopes.TEST.plus += configurations.functTestCompile
//    scopes.TEST.plus += configurations.functTestRuntime
    }
}