plugins {
    id 'war'
    id "org.gretty" version "3.0.6"
    id 'groovy'
}

configurations {
    functTestCompile.extendsFrom testImplementation
    functTestRuntime.extendsFrom testRuntimeOnly
}

ext.seleniumGroup = 'org.seleniumhq.selenium'
ext.seleniumVersion = '3.141.59'

dependencies {
    implementation project(':model')
    implementation project(':repository')
    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
    runtimeOnly 'javax.servlet:jstl:1.2'
    testImplementation 'org.codehaus.groovy:groovy:2.4.21'
    testImplementation 'junit:junit:4.13.2'
    functTestCompile 'org.codehaus.geb:geb-junit4:0.7.2'
    functTestCompile "org.gebish:geb-core:5.0-milestone-2"
    functTestRuntime "$seleniumGroup:selenium-firefox-driver:$seleniumVersion"
    functTestRuntime "$seleniumGroup:selenium-chrome-driver:$seleniumVersion"
    functTestCompile "$seleniumGroup:selenium-firefox-driver:$seleniumVersion"
    functTestCompile "$seleniumGroup:selenium-support:$seleniumVersion"
}

sourceSets {
    functionalTest {
        groovy.srcDir file('src/functTest/groovy')
        resources.srcDir file('src/functTest/resources')
        compileClasspath = sourceSets.main.output + configurations.functTestCompile
        runtimeClasspath = output + compileClasspath + configurations.functTestRuntime
    }
}

//When you apply gretty plugin, it adds gretty extension to the project.
//You can (but you donâ€™t have to) customize every aspect of gretty plugin with the help of gretty DSL:
gretty {
    servletContainer = 'jetty9'
    httpPort=8080
    contextPath='todo'
    // name of existing gradle task,which gretty "encloses" with servlet-container start/stop.
    integrationTestTask = 'functionalTest'
}

task functionalTest(type: Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath

    reports.html.outputLocation.fileValue reports.html.outputLocation.dir("functional").get().asFile
    reports.junitXml.outputLocation.fileValue reports.junitXml.outputLocation.dir("functional").get().asFile

    systemProperty 'geb.env', 'firefox'
//    systemProperty 'geb.env', 'chrome'
    systemProperty 'geb.build.reportsDir', reporting.file("$name/geb")
}

check.dependsOn functionalTest